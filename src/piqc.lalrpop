use ast::expr::*;
use ast::func::*;
use ast::prog::*;
use ast::stmt::*;
use ast::type_::*;

grammar;

Type: Type = {
    "int" => Type::Int,
    "float" => Type::Float,
    "bool" => Type::Bool,
};

Int: i32 = {
    r"\d+" => <>.parse().unwrap(),
};

Float: f32 = {
    r"\d*\.\d*" => <>.parse().unwrap(),
};

Bool: bool = {
    "true" => true,
    "false" => false,
};

UnaryOp: UnaryOp = {
    "-" => UnaryOp::Negate,
    "~" => UnaryOp::BitNot,
    "!" => UnaryOp::LogicalNot,
};

MultiplicativeOp: BinaryOp = {
    "*" => BinaryOp::Mul,
};

AdditiveOp: BinaryOp = {
    "+" => BinaryOp::Add,
    "-" => BinaryOp::Sub,
};

ShiftOp: BinaryOp = {
    "<<" => BinaryOp::Shl,
    ">>" => BinaryOp::Shr,
};

BitAndOp: BinaryOp = {
    "&" => BinaryOp::BitAnd,
};

BitXorOp: BinaryOp = {
    "^" => BinaryOp::BitXor,
};

BitOrOp: BinaryOp = {
    "|" => BinaryOp::BitOr,
};

ComparisonOp: BinaryOp = {
    "==" => BinaryOp::Eq,
    "!=" => BinaryOp::Ne,
    "<" => BinaryOp::Lt,
    ">" => BinaryOp::Gt,
    "<=" => BinaryOp::Le,
    ">=" => BinaryOp::Ge,
};

LogicalAndOp: BinaryOp = {
    "&&" => BinaryOp::LogicalAnd,
};

LogicalOrOp: BinaryOp = {
    "||" => BinaryOp::LogicalOr,
};

Identifier: Identifier = {
    @L r"[A-Za-z_][0-9A-Za-z_]*" @R => Identifier::new(<>),
};

IntLiteral: IntLiteral = {
    @L Int @R => IntLiteral::new(<>),
};

FloatLiteral: FloatLiteral = {
    @L Float @R => FloatLiteral::new(<>),
};

BoolLiteral: BoolLiteral = {
    @L Bool @R => BoolLiteral::new(<>),
};

PrimaryExpr: Expr = {
    IntLiteral => Expr::IntLiteral(<>),
    FloatLiteral => Expr::FloatLiteral(<>),
    BoolLiteral => Expr::BoolLiteral(<>),
    Identifier => Expr::Identifier(<>),
    "(" <Expr> ")" => <>,
};

UnaryExpr: Expr = {
    PrimaryExpr,
    @L UnaryOp UnaryExpr @R => UnaryExpr::new(<>),
};

BinaryExpr<Op, PrevExpr>: Expr = {
    PrevExpr,
    @L BinaryExpr<Op, PrevExpr> Op PrevExpr @R => BinaryExpr::new(<>),
};

MultiplicativeExpr = BinaryExpr<MultiplicativeOp, UnaryExpr>;

AdditiveExpr = BinaryExpr<AdditiveOp, MultiplicativeExpr>;

ShiftExpr = BinaryExpr<ShiftOp, AdditiveExpr>;

BitAndExpr = BinaryExpr<BitAndOp, ShiftExpr>;

BitXorExpr = BinaryExpr<BitXorOp, BitAndExpr>;

BitOrExpr = BinaryExpr<BitOrOp, BitXorExpr>;

ComparisonExpr = BinaryExpr<ComparisonOp, BitOrExpr>;

LogicalAndExpr = BinaryExpr<LogicalAndOp, ComparisonExpr>;

LogicalOrExpr = BinaryExpr<LogicalOrOp, LogicalAndExpr>;

Expr: Expr = {
    LogicalOrExpr,
};

DeclStmt: DeclStmt = {
    <@L> <Type> <Identifier> "=" <Expr> ";" <@R> => DeclStmt::new(<>),
};

AssignStmt: AssignStmt = {
    <@L> <Identifier> "=" <Expr> ";" <@R> => AssignStmt::new(<>),
};

ReturnStmt: ReturnStmt = {
    <@L> "return" ";" <@R> => ReturnStmt::new(<>),
};

BlockStmt: BlockStmt = {
    <@L> "{" <Stmt*> "}" <@R> => BlockStmt::new(<>),
};

Stmt: Stmt = {
    DeclStmt => Stmt::DeclStmt(<>),
    AssignStmt => Stmt::AssignStmt(<>),
    ReturnStmt => Stmt::ReturnStmt(<>),
    BlockStmt => Stmt::BlockStmt(<>),
};

Param: Param = {
    @L Type Identifier @R => Param::new(<>),
};

Params_: Vec<Param> = {
    Param => vec![<>],
    <params: Params_> "," <param: Param> => {
        let mut params = params;
        params.push(param);
        params
    },
};

Params: Params = {
    @L Params_? @R => Params::new(<>),
};

Func: Func = {
    <@L> "fn" <Identifier> "(" <Params> ")" <BlockStmt> <@R> => Func::new(<>),
};

pub Prog: Prog = {
    <Func*> => Prog::new(<>),
};
